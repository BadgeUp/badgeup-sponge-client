plugins {
	id 'java'
	id 'maven'
	id 'com.github.johnrengelman.shadow' version '1.2.3'
	id 'org.spongepowered.plugin' version '0.6'
}

sponge {
    plugin {
        id = 'badgeup'
		meta {
			name = 'BadgeUp Sponge Client'
			url = 'https://www.badgeup.io/'
			authors = ['Robert Herhold']
		}
    }
}

group = 'io.badgeup'
version = '2.1.0'
description = 'Easily add Achievements and Awards to your Sponge server'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'http://repo.spongepowered.org/maven' }
    maven { url 'https://jitpack.io' }
}

dependencies {
	compile 'org.spongepowered:spongeapi:6.0.0-SNAPSHOT'
	compile 'com.mashape.unirest:unirest-java:1.4.9'
    compile 'org.json:json:20160212'
}

shadowJar {
    baseName = 'badgeup-sponge-client'
    classifier = null
    version = 'v' + version

	// Relocate apache packages because of Forge
	relocate 'org.apache', 'relocated.org.apache'
	// Relocate JSON lib because of incompatibilities with modpacks like FTB Beyond
	relocate 'org.json', 'relocated.org.json'
    dependencies {
        exclude(dependency('org.spongepowered:spongeapi'))
    }
}

artifacts {
    archives shadowJar
}
